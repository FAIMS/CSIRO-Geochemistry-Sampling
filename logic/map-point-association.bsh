/************************** MAP (POINT ASSOCIATION) ***************************/
// TODO: Only show point whose uuid is equal to current uuid

void initTabGroupMap(String ref) {
  setMapZoom(ref, 12.0f);
  setMapFocusPoint(
      ref,
      549343.384492047,
      6555585.5887833 // Centre of points in 'spatialite.3857'
  );
  //int id = createCanvasLayer(ref, "Data Entry Layer");
  //setSelectedLayer(ref, id);
  //DATA_ENTRY_IDS.put(ref, id);

  showDatabaseLayers(ref);

  //showBaseMap(ref, "orthophoto 5m", "files/data/maps/orthophoto5m.3857.tif");
  //showBaseMap(ref, "orthophoto 10m", "files/data/maps/orthophoto10m.3857.tif");
  //showRasterMap(ref, "sentinel2", "files/data/maps/sentinel2.3857.tif");

  ps = createPointStyle(0, Color.CYAN, 0.2f, 1f);
  ls = createLineStyle(0, Color.GREEN, 0.05f, 0.3f, null);
  pos = createPolygonStyle(0, Color.parseColor("#440000FF"), createLineStyle(10, Color.parseColor("#AA000000"), 0.01f, 0.3f, null));
  ts = createTextStyle(0, Color.WHITE, 40, Typeface.SANS_SERIF);
  table = "RoadClipped";
  idcolumn = "PK_UID";
  labelcolumn = "NAME";
  showSpatialLayer(ref, "RoadClipped", "files/data/maps/spatialite.maps.3857.db", table, idcolumn, labelcolumn, ps, ls, pos, ts);

  ps = createPointStyle(0, Color.MAGENTA, 0.2f, 1f);
  ls = createLineStyle(0, Color.GREEN, 0.05f, 0.3f, null);
  pos = createPolygonStyle(0, Color.parseColor("#440000FF"), createLineStyle(10, Color.parseColor("#AA000000"), 0.01f, 0.3f, null));
  ts = createTextStyle(0, Color.WHITE, 40, Typeface.SANS_SERIF);
  table = "Sample_LocationsSWYilgarn";
  idcolumn = "PK_UID";
  labelcolumn = "Target";
  showSpatialLayer(ref, "Sample_LocationsSWYilgarn", "files/data/maps/spatialite.maps.3857.db", table, idcolumn, labelcolumn, ps, ls, pos, ts);
}

void updateMapUuid() {
  String tabGroup = getDisplayedTabGroup();
  if (SAMPLE_TYPES.contains(tabGroup))
    insertIntoLocalSettings("map-uuid", getUuid());
  else
    deleteFromLocalSettings("map-uuid");
}

String getMapRef(String tabGroupRef) {
  return tabGroupRef + "/Map/Map";
}

boolean geoListHasInstanceOf(List geometry, Class clazz) {
  for (Geometry g : geometry)
    if (clazz.isInstance(g))
      return true;
  return false;
}

// Associates the currently displayed record (i.e. tab group) with the point
// highlighted in the map.
void saveSelectedPoint() {
  String mapTabGroup = getDisplayedTabGroup();
  String mapRef      = getMapRef(mapTabGroup);
  List   geom        = getGeometryHighlights(mapRef);

  boolean isValidGeom = true;
  isValidGeom = isValidGeom && geom != null;
  isValidGeom = isValidGeom && geom.size() == 1;
  isValidGeom = isValidGeom && geoListHasInstanceOf(geom, Point.class);

  if (!isValidGeom) {
    showToast("Select exactly one point");
    return;
  }

  SaveCallback saveCallback  = new SaveCallback() {
    void onSave(uuid, newRecord) {
      clearGeometryHighlights(mapRef);

      refreshMap(mapRef);

      clearGpsInTabGroup(mapTabGroup);
      fillInGPS(mapTabGroup);

      showToast("Point saved");
    }
  };

  triggerAutoSave();
  saveArchEnt(
      getUuid(getDisplayedTabGroup()),
      getArchEntType(getDisplayedTabGroup()),
      geom,
      null,
      saveCallback
  );
}

for (String sampleType : SAMPLE_TYPES) {
  String ref = getMapRef(sampleType);
  initTabGroupMap(ref);
}

for (String sampleType : SAMPLE_TYPES) {
  String ref = sampleType + "/Map/Save_Selected_Geometry";
  addOnEvent(ref, "click", "saveSelectedPoint()");
}

for (String sampleType : SAMPLE_TYPES) {
  String refBtn = sampleType + "/Map/Center_Me_1";
  String refMap = getMapRef(sampleType);
  addOnEvent(refBtn, "click", "centerOnCurrentPosition", new Object[] {refMap});
}

for (String sampleType : SAMPLE_TYPES) {
  String refMap = getMapRef(sampleType);
  addOnEvent(getTabRef(refMap), "show",  "refreshMap", new Object[] {refMap});
}

for (String sampleType : SAMPLE_TYPES) {
  addOnEvent(sampleType, "create", "updateMapUuid()");
  addOnEvent(sampleType, "copy",   "updateMapUuid()");
  addOnEvent(sampleType, "fetch",  "updateMapUuid()");
  addOnEvent(sampleType, "leave",  "updateMapUuid()");
  addOnEvent(sampleType, "save",   "updateMapUuid()");
}
